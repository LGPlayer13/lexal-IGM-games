Let's make a game!
 name:Lemonade Clicker
 by:lex
 created:04/01/2023
 updated:19/03/2023
 version:1
 desc:Make some lemonade in this ripoff of Cookie Clicker!<//><b>Version 1.0!</b><.>added oranges! these citrusy fellows are here to make you stupidly rich<.>less exciting, but reworked whole game's code for more streamlined updates. that means more content, for you<.>old game still exists, but those saves wont work here. sorry!<.>added_feature6<.>added a new building! something something song of storms

Settings

 spritesheet:i, 48 by 48, https://raw.githubusercontent.com/Lexal1/lemon-clicker/main/lemons-export.png
 spritesheet:ach, 48 by 48, https://raw.githubusercontent.com/Lexal1/lemon-clicker/main/achs-export.png
 building cost increase:125%

CSS

  #game
  {
    background:black;
  }
  #game. .thing.owned
  {
    background:gray;
  }
  #game. .thing.notOwned 
  {
    background:black;
  }
  #game.filtersOn .thing.cantAfford.notOwned
  {
    filter:blur(2px);
    background:black;
  }

Buttons

 *lemonbutton
  on click:gain 1 lemon
  on click:if (have limeunlock) gain 1 lime
  on click:if (have orangeunlock) gain 0.1 orange
  class:bigButton
  icon:https://i.postimg.cc/fysgh0zw/lemone-leome.png
  no tooltip

Includes
////////////////////////////////////////////////////////////////////////////////////BUILDINGUPGRADES

 *include boxup %id %name %flavor="There's nothing interesting here." %req %cost %icon="0"
  *boxup[%id]
    name:[%name]
    desc:Boxes are <b>twice</b> as efficient.<q>"[%flavor]"</q>
    req:[%req] box
    cost:[%cost]
    passive:multiply lemon yield of box by 2
    icon:i[[%icon],5]
    
 *include standup %id %name %flavor="There's nothing interesting here." %req %cost %icon="0"
  *standup[%id]
    name:[%name]
    desc:Stands are <b>twice</b> as efficient.<q>"[%flavor]"</q>
    req:[%req] stand
    cost:[%cost]
    passive:multiply lemon yield of stand by 2
    icon:i[[%icon],6]
    
 *include shopup %id %name %flavor="There's nothing interesting here." %req %cost %icon="0"
  *shopup[%id]
    name:[%name]
    desc:Boxes are <b>twice</b> as efficient.<q>"[%flavor]"</q>
    req:[%req] shop
    cost:[%cost]
    passive:multiply lemon yield of shop by 2
    passive:multiply lime yield of shop by 2
    icon:i[[%icon],7]
    
 *include vendup %id %name %flavor="There's nothing interesting here." %req %cost %icon="0"
  *vendup[%id]
    name:[%name]
    desc:Vending Machines are <b>twice</b> as efficient.<q>"[%flavor]"</q>
    req:[%req] vend
    cost:[%cost]
    passive:multiply lemon yield of vend by 2
    passive:multiply lime yield of vend by 2
    icon:i[[%icon],8]
    
 *include weathup %id %name %flavor="There's nothing interesting here." %req %cost %icon="0"
  *weathup[%id]
    name:[%name]
    desc:Weather Machines are <b>twice</b> as efficient.<q>"[%flavor]"</q>
    req:[%req] weath
    cost:[%cost]
    passive:multiply lemon yield of weath by 2
    passive:multiply lime yield of weath by 2
    passive:multiply orange yield of weath by 2
    icon:i[[%icon],9]
    
////////////////////////////////////////////BEVERAGES

 *include lemonade %id %name %flavor="There's nothing interesting here." %req %cost %icon="0"
  *lemonade[%id]
   name:[%name]
   desc:Lemon production +1%.<q>"[%flavor]"</q>
   passive:multiply yield of lemons by 1.01
   cost:[%cost] lemons
   req:[%req] lemons:earned
   icon:i[[%icon],2]
   
 *include limeade %id %name %flavor="There's nothing interesting here." %req %cost %icon="0"
  *limeade[%id]
   name:[%name]
   desc:Lime production +1%.<q>"[%flavor]"</q>
   passive:multiply yield of limes by 1.01
   cost:[%cost] limes
   req:[%req] limes:earned
   icon:i[[%icon],3]
   
 *include orangeade %id %name %flavor="There's nothing interesting here." %req %cost %icon="0"
  *orangeade[%id]
   name:[%name]
   desc:Orange production +1%.<q>"[%flavor]"</q>
   passive:multiply yield of oranges by 1.01
   cost:[%cost] orange
   req:[%req] oranges:earned
   icon:i[[%icon],4]
   
///////////////////////////////////ACHS

////////////////////TOTAL AND PER SECOND
 *include totlemach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *totlemach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] lemon:earned
   icon:ach[[%icon],0]
   
 *include lempsach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *lempsach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] lemon:ps
   icon:ach[[%icon],0]
   
 *include totlimeach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *totlimeach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] lime:earned
   icon:ach[[%icon],1]
   
 *include limepsach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *limepsach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] lime:ps
   icon:ach[[%icon],1]
   
*include totoraach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *totoraach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] orange:earned
   icon:ach[[%icon],2]
   
*include orapsach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *orapsach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] orange:ps
   icon:ach[[%icon],2]  
   
///////////////////////////BUILDING ACHS
 *include boxach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *boxach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] box
   icon:ach[[%icon],3]
 *include standach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *standach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] stand
   icon:ach[[%icon],4]
 *include shopach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *shopach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] shop
   icon:ach[[%icon],5]
 *include vendach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *vendach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] vend
   icon:ach[[%icon],6]
 *include weathach %id %name %desc="There's nothing interesting here." %req %icon="0"
  *weathach[%id]
   name:[%name]
   desc:[%desc]
   req:[%req] weath
   icon:ach[[%icon],7]

Resources

 *lemon|lemons
  show earned
  name:Lemons
  desc:Juiced <b>[lemon:earned] lemons</b> total.
  icon:i[0,0]
 
 *lime|limes
  show earned
  name:Limes
  desc:Squeezed <b>[lime:earned] limes</b> total.
  hidden when 0
  icon:i[1,0]
 
 *orange|oranges
  show earned
  hidden when 0
  desc:Squashed <b>[orange:earned] oranges</b> total.
  name:Oranges
  icon:i[2,0]

 *lemonps
  is always:(lemon:ps)
  hidden

 *limeps
  is always:(lime:ps)
  hidden

 *hiddene
  on start:hide this

Buildings

 *lemonadestand1|box
  name:Lemonade Cardboard Box
  on tick:gain 0.1 lemon
  desc:A basic cardboard box to sell lemonade from.
  req:1 lemon:earned
  cost:15 lemons
  icon:i[0,1]

 *lemonadestand2|stand
  name:Lemonade Stand
  on tick:gain 1 lemon
  desc:A small wooden stand to sell more lemonade.
  cost:200 lemons
  req:100 lemons:earned
  icon:i[1,1]

//buy with lemons and limes

 *lemonadestand3|shop
  name:Lemonade Store
  on tick:gain 35 lemons
  on tick:gain 1 lime
  desc:A big store to sell a lot more lemonade.
  cost:4000 lemons, 50 limes
  req:15 lime:earned
  icon:i[2,1]

 *lemonadestand4|vend
  name:Vending Machine
  on tick:gain 165 lemons
  on tick:gain 5 limes
  desc:A portable vending machine to sell lots of lemonade.
  cost:55000 lemons, 7800 limes
  req:1e4 limes:earned
  icon:i[3,1]

//limes and oranges

 *lemonadestand5|weath
  name:Weather Machine
  on tick:gain 350 lemons
  on tick:gain 25 limes
  on tick:gain 1 orange
  desc:A machine to make hot days to sell a bunch of lemonade.
  cost:5500 limes, 45 oranges
  req:5 oranges:earned
  icon:i[4,1]




Upgrades
/////////////////////////////BUILDING UPGRADES
 [include boxup %id="1" %name="Water resistant boxes" %flavor="These seem to last quite a bit longer than regular boxes. Who'da thunk that leaving a cardboard box out in the rain would be a bad idea?" %req="1" %cost="100 lemons" %icon="0"]
 [include boxup %id="2" %name="Recycling" %flavor="If you recycle old boxes, you get extra money, which means more lemons! Score!" %req="5" %cost="2000 lemons" %icon="1"]
 [include boxup %id="3" %name="Folding chair" %flavor="Now you don't have to sit on the ground looking like a hobo." %req="25" %cost="200000 lemons" %icon="2"]
 [include boxup %id="4" %name="Sugar cookies" %flavor="Makes the lemonade taste worse, but hey, at least they taste good!" %req="50" %cost="2000000 lemons" %icon="3"]
 
 [include standup %id="1" %name="Coat of paint" %flavor="Now they look far more appealing." %req="1" %cost="1000 lemons" %icon="0"]
 [include standup %id="2" %name="Flashier signs" %flavor="These have fairy lights and glow in the dark marker!" %req="5" %cost="24000 lemons" %icon="1"]
 [include standup %id="3" %name="Turnstiles" %flavor="Customers passing you by? Install this, and they'll never pass by again!" %req="25" %cost="240000 lemons" %icon="2"]
 [include standup %id="4" %name="A duck" %flavor="The duck walked up to the stand and started asking for grapes. Maybe you could use it as a mascot or something?" %req="50" %cost="240000 lemons" %icon="3"]

 [include shopup %id="1" %name="Revolving doors" %flavor="speen" %req="1" %cost="3400 limes" %icon="0"]
 [include shopup %id="2" %name="Bigger shelves" %flavor="For bigger lemonades, for bigger profits." %req="5" %cost="68000 limes" %icon="1"]
 [include shopup %id="3" %name="Jim" %flavor="...from accounting." %req="25" %cost="680000 limes" %icon="2"]
 [include shopup %id="4" %name="Better advertising" %flavor="Even shows up in the newspapers!" %req="50" %cost="680000 limes" %icon="3"]

 [include vendup %id="1" %name="Accept change" %flavor="You wouldn't happen to have a penny on you, would ya?" %req="1" %cost="5700 limes" %icon="0"]
 [include vendup %id="2" %name="Faulty despensers" %flavor="Bonus points if you don't add a clear front to make them think that it's not your fault." %req="5" %cost="140000 limes" %icon="1"]
 [include vendup %id="3" %name="No refunds" %flavor="Now your customers won't complain (as much)!" %req="25" %cost="1400000 limes" %icon="2"]
 [include vendup %id="4" %name="Cages" %flavor="No, these aren't for keeping the machines safe, why do you ask?" %req="50" %cost="1400000 limes" %icon="3"]

 [include weathup %id="1" %name="Optimal growing weather" %flavor="It's amazing how much faster citrus grows when you allow seasons to exist." %req="1" %cost="630 oranges" %icon="0"]
 [include weathup %id="2" %name="Selling out" %flavor="Now, now, hear me out. If you sell the services of your weather machines, you can make a fortune to buy more lemons." %req="5" %cost="9600 oranges" %icon="1"]
 [include weathup %id="3" %name="" %flavor="" %req="5" %cost="960000 oranges" %icon="2"]


//////////////////////////////////////BEVERAGES

 [include lemonade %id="1" %name="Lemonade" %flavor="We all gotta start somewhere. No ice?!" %req="100000" %cost="999999" %icon="0"]
 [include lemonade %id="2" %name="Tornado Lemonade" %flavor="A blend of lemon and fizzy soda served in a tornado glass!" %req="1e7" %cost="3e7" %icon="1"]
 [include lemonade %id="3" %name="Lemon-lime Lemonade" %flavor="Not to be confused with lime-lemon limeade." %req="1e7" %cost="3e7" %icon="2"]
 [include lemonade %id="4" %name="D.I.Y Lemonade" %flavor="...It's just a packet of sugar and some lemons..." %req="1e8" %cost="7e8" %icon="3"]

 [include limeade %id="1" %name="Limeade" %flavor="Very refreshing! And mildly zesty." %req="100000" %cost="999999" %icon="0"]
 [include limeade %id="2" %name="Sourade" %flavor="Ouch! That really puts hair on your tounge..." %req="1e7" %cost="3e7" %icon="1"]
 [include limeade %id="3" %name="Lime-lemon Limeade" %flavor="Not to be confused with lemon-lime lemonade." %req="1e7" %cost="3e7" %icon="2"]
 [include limeade %id="4" %name="Limeade 2" %flavor="Now with electric boogaloos!" %req="1e7" %cost="5e8" %icon="0"]

 *limeunlock
  name:Fantastic Limes and How To Find Them
  desc:<b>Unlocks limes,</b> boosts clicking power by a large amount.<q>"Limes are very zesty used in food as a flavoring, or, if juiced, very thirst quenching."</q>
  req:totlemach3
  cost:2e6 lemons
  passive:increase lemon yield of lemonbutton by lemonps
  icon:i[1,0]

 *orangeunlock
  name:Hero's Guide to Oranges
  desc:<b>Unlocks oranges,</b> boosts lemon and lime clicking power by a large amount.<q>"Oranges are a staple for snacks, orange juice, and fighting off scurvy."</q>
  req:totlimeach3
  cost:1e7 lemons, 5e5 limes
  passive:increase lemon yield of lemonbutton by lemonps
  passive:increase lime yield of lemonbutton by limeps
  icon:i[2,0]

 /*choice1
  desc:choice1_desc_choosewisely
  req:totorangeach3
  cost:666 oranges
  on earn:
   hide choice2
   toast choice_sealed_toast_dia
   increase hiddene by 6
  end
  icon:i[6,6]

 *choice2
  desc:choice2_desc_choosewisely
  req:totorangeach3
  cost:666 oranges
  on earn:
   hide choice1
   toast choice_sealed_toast_dia
   increase hiddene by 66
  end
  icon:i[6,6]*/

Achievements
//////////////////////////////////////LEMONS
 [include totlemach %id="1" %name="When life gives you lemons" %desc="Make 1 lemon." %req="1" %icon="0"]
 [include totlemach %id="2" %name="Tight squeeze" %desc="Make 1,000 lemons." %req="1000" %icon="1"]
 [include totlemach %id="3" %name="Juicy" %desc="Make 1 million lemons." %req="1e6" %icon="2"]
 [include totlemach %id="4" %name="Sour taste" %desc="Make 100 million lemons." %req="1e8" %icon="3"]
 [include totlemach %id="5" %name="Juice all the things" %desc="Make 1 billion lemons." %req="1e9" %icon="4"]
 [include totlemach %id="6" %name="How many lemons have you made?" %desc="Make 100 billion lemons.<q>I am not really a math guy, you know?</q>" %req="1e11" %icon="5"]
 [include totlemach %id="7" %name="The whole world is covered in lemons" %desc="Make 1 trillion lemons." %req="1e12" %icon="6"]
 [include totlemach %id="8" %name="And not one of them is mine" %desc="Make 100 trillion lemons." %req="1e14" %icon="7"]

 [include lempsach %id="1" %name="I like lemons" %desc="Make 1 lemon per second." %req="1" %icon="0"]
 [include lempsach %id="2" %name="The journey of 1,000 lemonades" %desc="Make 10 lemons per second." %req="10" %icon="1"]
 [include lempsach %id="3" %name="That's a lotta lemons" %desc="Make 100 lemons per second." %req="100" %icon="2"]
 [include lempsach %id="4" %name="Do you want to build a lemonade stand" %desc="Make 1,000 lemons per second." %req="1000" %icon="3"]
 [include lempsach %id="5" %name="Gas gas gas" %desc="Make 1 million lemons per second." %req="1e6" %icon="4"]
 [include lempsach %id="6" %name="They don't grow on trees" %desc="Make 10 million lemons per second.<q>Wait. They do grow on trees? You're kidding, right?</q>" %req="1e7" %icon="5"] 
 [include lempsach %id="7" %name="It's not maschocism" %desc="Make 100 million lemons per second." %req="1e8" %icon="6"]
 [include lempsach %id="8" %name="Lemons make the world go round" %desc="Make 1 billion lemons per second.<q>And what goes around, comes around! Wait, I've confused myself.</q>" %req="1e9" %icon="7"]

//////////////////////////////////////////////LIMES

 [include totlimeach %id="1" %name="Once upon a lime" %desc="Make 1 lime." %req="1" %icon="0"]
 [include totlimeach %id="2" %name="Need a juicer?" %desc="Make 1,000 limes." %req="1000" %icon="1"]
 [include totlimeach %id="3" %name="2.8 pH" %desc="Make 1 million limes." %req="1e6" %icon="2"]
 [include totlimeach %id="4" %name="Sublime" %desc="Make 100 million limes." %req="1e8" %icon="3"]
 [include totlimeach %id="5" %name="Make life take lemons back" %desc="Make 1 billion limes.<q>Say I don't want your damn lemons!</q>" %req="1e9" %icon="4"]
 [include totlimeach %id="6" %name="Eyup" %desc="Make 100 billion limes." %req="1e11" %icon="5"]
 [include totlimeach %id="7" %name="I think you've gone too far" %desc="Make 1 trillion limes." %req="1e12" %icon="6"]
 [include totlimeach %id="8" %name="No, really, you've gone too far" %desc="Make 100 trillion limes." %req="1e14" %icon="7"]
 
 [include limepsach %id="1" %name="What're those" %desc="Make 1 lime per second." %req="1" %icon="0"]
 [include limepsach %id="2" %name="Keeping in lime" %desc="Make 10 limes per second." %req="10" %icon="1"]
 [include limepsach %id="3" %name="Citrus entanglement" %desc="Make 100 limes per second." %req="100" %icon="2"] 
 [include limepsach %id="4" %name="I love the smell of limes in the morning" %desc="Make 1,000 limes per second." %req="1000" %icon="3"] 
 [include limepsach %id="5" %name="Don't give me that" %desc="Make 1 million limes per second." %req="1e6" %icon="4"] 
 [include limepsach %id="6" %name="Has appeal" %desc="Make 10 million limes per second." %req="1e7" %icon="5"] 
 [include limepsach %id="7" %name="Top of the lime jokes" %desc="Make 100 million limes per second.<q>Only quality here, I swear.</q>" %req="1e8" %icon="6"] 
 [include limepsach %id="8" %name="Limeited time on this earth" %desc="Make 1 billion limes per second." %req="1e9" %icon="7"]
 
///////////////////////////////////////////////////ORANGES

 [include totoraach %id="1" %name="No rhymes here, I mean it" %desc="Make 1 orange.<q>Anybody want a peanut?</q>" %req="1" %icon="0"]
 [include totoraach %id="2" %name="First orange of the day" %desc="Make 1,000 oranges." %req="1000" %icon="1"]
 [include totoraach %id="3" %name="Ow my eyes" %desc="Make 1 million oranges." %req="1e6" %icon="2"]
 [include totoraach %id="4" %name="I think you have problems" %desc="Make 100 million oranges." %req="1e8" %icon="3"]
 [include totoraach %id="5" %name="It's oranging time" %desc="Make 1 billion oranges.<q>Wait, didn't we already make this joke?</q>" %req="1e9" %icon="4"]
 [include totoraach %id="6" %name="Oh hey, you're still here" %desc="Make 100 billion oranges." %req="1e11" %icon="5"]
 [include totoraach %id="7" %name="We can all avoid scurvy if we all eat an orange" %desc="Make 1 trillion oranges.<q>Well, gee, I feel kinda guilty now.</q>" %req="1e12" %icon="6"]
 [include totoraach %id="8" %name="Orange tinted glasses" %desc="Make 100 trillion oranges." %req="1e14" %icon="7"]
 
 [include orapsach %id="1" %name="At your expense" %desc="Make 1 orange per second." %req="1" %icon="0"]
 [include orapsach %id="2" %name="I hate oranges" %desc="Make 10 oranges per second.<q>They're smooth and soft and irritating.</q>" %req="10" %icon="1"]
 [include orapsach %id="3" %name="Fast" %desc="Make 100 oranges per second.<q>Ironically this is quite slow.</q>" %req="100" %icon="2"] 
 [include orapsach %id="4" %name="Orange you glad that it isn't lemonade" %desc="Make 1,000 oranges per second." %req="1000" %icon="3"] 
 [include orapsach %id="5" %name="Rolling around" %desc="Make 1 million oranges per second.<q>(at the speed of sound)</q>" %req="1e6" %icon="4"] 
 [include orapsach %id="6" %name="I've run out of jokes" %desc="Make 10 million oranges per second." %req="1e7" %icon="5"] 
 [include orapsach %id="7" %name="SPEEDRUN" %desc="Make 100 million oranges per second." %req="1e8" %icon="6"] 
 [include orapsach %id="8" %name="I forgorange" %desc="Make 1 billion oranges per second." %req="1e9" %icon="7"]
 
//////////////////////////////?///////////////////////BUILDINGS

 [include boxach %id="1" %name="Boxed in" %desc="Have 1 box." %req="1" %icon="0"]
 [include boxach %id="2" %name="Boxing match" %desc="Have 50 boxes." %req="50" %icon="1"]
 [include boxach %id="3" %name="Um actually it's a cuboid" %desc="Have 100 boxes." %req="100" %icon="2"]
 [include boxach %id="4" %name="Moving out" %desc="Have 150 boxes." %req="150" %icon="3"]
 [include boxach %id="5" %name="All I see is boxes" %desc="Have 200 boxes." %req="200" %icon="4"]

 [include standach %id="1" %name="Standza" %desc="Have 1 stand." %req="1" %icon="0"]
 [include standach %id="2" %name="Standing upright" %desc="Have 50 stands." %req="50" %icon="1"]
 [include standach %id="3" %name="Got a nickel?" %desc="Have 100 stands." %req="100" %icon="2"]
 [include standach %id="4" %name="Outstanding" %desc="Have 150 stands." %req="150" %icon="3"]
 [include standach %id="5" %name="Just around the corner" %desc="Have 200 stands." %req="200" %icon="4"]

 [include shopach %id="1" %name="Capital purchase" %desc="Have 1 store." %req="1" %icon="0"]
 [include shopach %id="2" %name="Checkout" %desc="Have 50 stores." %req="50" %icon="1"]
 [include shopach %id="3" %name="Girl are you construction worker" %desc="Have 100 stores.<q>because you are building</q>" %req="100" %icon="2"]
 [include shopach %id="4" %name="Need for greed" %desc="Have 150 stores." %req="150" %icon="3"]
 [include shopach %id="5" %name="Wolf of Walmart" %desc="Have 200 stores." %req="200" %icon="4"]

 [include vendach %id="1" %name="Shake it" %desc="Have 1 vending machine." %req="1" %icon="0"]
 [include vendach %id="2" %name="Only 2 deaths so far" %desc="Have 50 vending machines." %req="50" %icon="1"]
 [include vendach %id="3" %name="Ka-clunk" %desc="Have 100 vending machines." %req="100" %icon="2"]
 [include vendach %id="4" %name="Stuck" %desc="Have 150 vending machines.<q>Again?</q>" %req="150" %icon="3"]
 [include vendach %id="5" %name="Only a dollar" %desc="Have 200 vending machines.<q>...and then he holds up a comically large dollar! Funniest thing I've ever seen!</q>" %req="200" %icon="4"]
 
 [include weathach %id="1" %name="Smell of rain" %desc="Have 1 weather machine." %req="1" %icon="0"]
 [include weathach %id="2" %name="It's raining rain" %desc="Have 50 weather machines.<q>What did you expect? Tacos?</q>" %req="50" %icon="1"]
 [include weathach %id="3" %name="Power over sky" %desc="Have 100 weather machines." %req="100" %icon="2"]
 [include weathach %id="4" %name="The trees are really sneezing today" %desc="Have 150 weather machines." %req="150" %icon="3"]
 [include weathach %id="5" %name="Thunder and lightning aren't so frightning" %desc="Have 200 weather machines." %req="200" %icon="4"]
